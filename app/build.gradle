plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	mavenLocal()
	flatDir {
		dirs  "${project(':baz:client').buildDir}/libs"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	implementation project(":foo:client")

	implementation "com.example.bar:client:1.0.0-SNAPSHOT"
	implementation name: "client-1.0.0-SNAPSHOT" //baz

}



task showSourceClasspath(){
	doLast {

		configurations
				.getByName(sourceSets.main.compileClasspathConfigurationName)
				.resolvedConfiguration
				.resolvedArtifacts
				.grep({it.file.name.startsWith("client")})
				.each {
					println """
					################################################################
					Name: $it.name
					Id:   $it.id / ${it.id.componentIdentifier.getClass().simpleName}
					File: $it.file
					################################################################
					""".stripIndent()
		}
	}
}